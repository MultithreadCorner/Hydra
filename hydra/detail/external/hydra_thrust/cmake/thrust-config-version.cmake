# Parse version information from version.h:
include("${CMAKE_CURRENT_LIST_DIR}/hydra_thrust-header-search.cmake")

file(READ "${_HYDRA_THRUST_VERSION_INCLUDE_DIR}/hydra_thrust/version.h" HYDRA_THRUST_VERSION_HEADER)
string(REGEX MATCH "#define[ \t]+HYDRA_THRUST_VERSION[ \t]+([0-9]+)" DUMMY "${HYDRA_THRUST_VERSION_HEADER}")
set(HYDRA_THRUST_VERSION_FLAT ${CMAKE_MATCH_1})
# Note that Thrust calls this the PATCH number, CMake calls it the TWEAK number:
string(REGEX MATCH "#define[ \t]+HYDRA_THRUST_PATCH_NUMBER[ \t]+([0-9]+)" DUMMY "${HYDRA_THRUST_VERSION_HEADER}")
set(HYDRA_THRUST_VERSION_TWEAK ${CMAKE_MATCH_1})

math(EXPR HYDRA_THRUST_VERSION_MAJOR "${HYDRA_THRUST_VERSION_FLAT} / 100000")
math(EXPR HYDRA_THRUST_VERSION_MINOR "(${HYDRA_THRUST_VERSION_FLAT} / 100) % 1000")
math(EXPR HYDRA_THRUST_VERSION_PATCH "${HYDRA_THRUST_VERSION_FLAT} % 100") # Thrust: "subminor" CMake: "patch"

set(HYDRA_THRUST_VERSION "${HYDRA_THRUST_VERSION_MAJOR}.${HYDRA_THRUST_VERSION_MINOR}.${HYDRA_THRUST_VERSION_PATCH}.${HYDRA_THRUST_VERSION_TWEAK}")

set(PACKAGE_VERSION ${HYDRA_THRUST_VERSION})
set(PACKAGE_VERSION_COMPATIBLE FALSE)
set(PACKAGE_VERSION_EXACT FALSE)
set(PACKAGE_VERSION_UNSUITABLE FALSE)

if(PACKAGE_VERSION VERSION_GREATER_EQUAL PACKAGE_FIND_VERSION)
  if(HYDRA_THRUST_VERSION_MAJOR VERSION_EQUAL PACKAGE_FIND_VERSION_MAJOR AND
     HYDRA_THRUST_VERSION_MINOR VERSION_GREATER_EQUAL PACKAGE_FIND_VERSION_MINOR)
    set(PACKAGE_VERSION_COMPATIBLE TRUE)
  endif()

  if(PACKAGE_FIND_VERSION VERSION_EQUAL PACKAGE_VERSION)
    set(PACKAGE_VERSION_EXACT TRUE)
  endif()
endif()
